{ installerTarget ? "isoImage", kexecRootSSHKey ? null }:

let
  nixpkgs = builtins.fetchGit {
    url = "https://github.com/NixOS/nixpkgs";
    ref = "nixos-22.11";
    rev = "7dc71aef32e8faf065cb171700792cf8a65c152d";
  };

  cleverca22NixTests = builtins.fetchGit {
    url = "https://github.com/cleverca22/nix-tests";
    ref = "master";
    rev = "2ba968302208ff0c17d555317c11fd3f06e947e2";
  };

  pkgs = import nixpkgs {};

  snsClusterConfigGit = nixosVersion:
    pkgs.stdenvNoCC.mkDerivation rec {
      name = "sns-cluster-config";

      src = ./.;

      nativeBuildInputs = with pkgs; [ makeWrapper ];
      buildInputs = with pkgs; [
        coreutils util-linux parted dosfstools zfs mount iproute2 gnused git
        envsubst nixosVersion
      ];

      buildPhase = ''
        echo "Checking whether there are any uncommitted changes or untracked files."
        git update-index --refresh
        git diff-index --quiet HEAD --
        test "$(git ls-files --exclude-standard --others | wc -l)" -lt 1

        # Remove files ignored by git
        git clean -dfX
      '';

      installPhase = ''
        cp -rf ./ $out
      '';

      postFixup = ''
        wrapProgram $out/sns-cluster-install.sh \
          --set PATH ${pkgs.lib.makeBinPath buildInputs}
      '';
    };

  isoImageSystemConfig = { modulesPath, ... }: {
    imports = [
      # Add in the standard module for NixOS install media:
      (modulesPath + "/installer/cd-dvd/installation-cd-minimal.nix")

      # Provide an initial copy of the NixOS channel so that the user
      # doesn't need to run "nix-channel --update" first.
      (modulesPath + "/installer/cd-dvd/channel.nix")
    ];
  };

  netbootSystemConfig = { modulesPath, ... }: {
    imports = [
      # Add in the standard module for NixOS install media:
      (modulesPath + "/installer/netboot/netboot.nix")

      # Avoid bloating the installer:
      (modulesPath + "/profiles/minimal.nix")
    ];

    # For now, we assume that this machine is netbooted through a
    # Serial-on-Lan connection, so direct kernel output to the
    # serial port. This also spawns a getty on ttyS0.
    boot.kernelParams = [
      "console=ttyS0,115200"
    ];
  };

  kexecSystemConfig = { ... }: {
    imports = [
      # Import cleverca22's kexec NixOS installer config
      "${cleverca22NixTests}/kexec/configuration.nix"
    ];

    # Enable DHCP on all interfaces:
    networking.useDHCP = true;

    kexec.autoReboot = false;

    users.users.root.openssh.authorizedKeys.keys = [
      kexecRootSSHKey
    ];
  };

  targetSystem = import "${nixpkgs}/nixos" {
    configuration = { config, pkgs, lib, modulesPath, ... }: {
      imports = (
        lib.optional
          (installerTarget == "isoImage")
          isoImageSystemConfig
      ) ++ (
        lib.optional
          (installerTarget == "netboot")
          netbootSystemConfig
      ) ++ (
        lib.optional
          (installerTarget == "kexec_tarball")
          kexecSystemConfig
      );

      # Configuration independent of targetFormat:

      # To ensure that we can actually use ZFS (and other potentially
      # required file systems) to perform the installation:
      boot.supportedFilesystems = [
        "zfs" "ext4" "vfat" "btrfs"
      ];

      # Required for ZFS:
      networking.hostId = "01234567";

      # Make a few useful utilities accessible:
      environment.systemPackages = with pkgs; [
        vim tmux htop nload parted gptfdisk ipmitool
      ];

      boot.kernelParams = [
        "module_blacklist=md_mod"
      ];

      # Keep the "nixos" user generated by the imports of some of the
      # installerTarget configurations, but auto-login as root. This
      # is an installer, there's no harm in that.
      services.getty.autologinUser = lib.mkForce "root";

      system.activationScripts."symlink-sns-cluster-config-root" = let
        nixosVersion =
          lib.findSingle
            (pkg: pkg.name == "nixos-version")
            null null
            config.environment.systemPackages;
        nixosVersionFoundAssert =
          lib.assertMsg
            (nixosVersion != null)
            "nixos-version not found in the target system's environment.systemPackages!";
      in ''
        # DUMMY: ensure the nixos-version assertion is evaluated:
        # ${builtins.toString nixosVersionFoundAssert}
        ln -s ${snsClusterConfigGit nixosVersion} /root/sns-cluster-config
      '';
    };
  };

in
  if installerTarget == "netboot" then
    # Build a top-level derivation which contains symlinks to the
    # kernel, initial ramdisk and a special IPXE script which
    # instructs the target host on how to load both these files.
    (pkgs.linkFarm "nixos-netboot" [ {
      name = "initrd";
      path = "${targetSystem.config.system.build.netbootRamdisk}/initrd";
    } {
      name = "bzImage";
      path = "${targetSystem.config.system.build.kernel}/bzImage";
    } {
      name = "netboot.ipxe";
      path = "${targetSystem.config.system.build.netbootIpxeScript}/netboot.ipxe";
    } ])
  else
    targetSystem.config.system.build."${installerTarget}"
